import org.gradle.execution.BuildConfigurationAction

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

def publishVersion = ""
def publishGroupId = ""
def publishArtifactId = ""

new File("${rootDir}/scripts/publish-version.txt").eachLine { line ->
    if (line.contains("VERSION")) {
        String[] split = line.split("=")
        publishVersion = split[1]
    }

    if (line.contains("GROUP_ID")) {
        String[] split = line.split("=")
        publishGroupId = split[1]
    }

    if (line.contains("ARTIF")) {
        String[] split = line.split("=")
        publishArtifactId = split[1]
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}



artifacts {
    archives androidSourcesJar
    archives javadocJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId publishGroupId
                artifactId publishArtifactId
                version publishVersion
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = 'shopiroller-uikit'
                    description = 'Mobile e-commerce applications are now closer to your users with Shopiroller iOS SDK. It is now very easy to have your own mobile application.'
                    url = 'https://github.com/shopiroller/shopiroller-uikit-android'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/shopiroller/shopiroller-uikit-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'hpolat'
                            name = 'Hasan Polat'
                            email = 'hasan.polat@mobiroller.com\n'
                        }
                        developer {
                            id = 'ayalcn'
                            name = 'Abdullah Yalçın'
                            email = 'abdullah.yalcin@mobiroller.com'
                        }
                        developer {
                            id = 'gorkemgur'
                            name = 'Görkem Göktuğ Gür'
                            email = 'gorkem.gur@mobiroller.com'
                        }
                        developer {
                            id = 'ealtaca'
                            name = 'Emre Can Altaca'
                            email = 'ealtaca@mobiroller.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:shopiroller/shopiroller-uikit-android.git'
                        developerConnection = 'scm:git@github.com:shopiroller/shopiroller-uikit-android.git'
                        url = 'https://github.com/shopiroller/shopiroller-uikit-android'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
